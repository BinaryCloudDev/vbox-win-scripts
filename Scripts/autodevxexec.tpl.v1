::######################################################################################
::######################################################################################
::######################################################################################
::######################################################################################
:: This script expects that environment variable DEVX_EXEC is the path to devxexec.exe
::######################################################################################
set OPT=%*
if defined OPT call :main_autodevxexec %*
goto :EOF


:main_autodevxexec
if not defined DEVX_EXEC (
	echo Sorry the environmental variable %%DEVX_EXEC%% has not been set.  Exiting!
	goto :EOF
) ELSE (
if not exist "%DEVX_EXEC%" (
	echo Sorry the environmental variable %%DEVX_EXEC%% doesn't reference an existing file.  Exiting!
	goto :EOF
))

call :parseSwitches %*
call :retrievePass

if     defined User        echo User     ::: %User%
if     defined Domain      echo Domain   ::: %Domain%
if     defined Password    echo Password ::: %Password%
if     defined EncodedPass echo Encoded  ::: %EncodedPass%

if     defined Password    ^
if     defined DEVX_EXEC   "%DEVX_EXEC%" %switches%

if not defined Password    ^
if     defined EncodedPass ^
if     defined DEVX_EXEC   "%DEVX_EXEC%" /password:%EncodedPass% %switches%

goto :EOF

::######################################################################################
::######################################################################################
::######################################################################################
::######################################################################################
::Functions

:encodePass
	set EncodedPass_%1=%2
	goto :EOF

:retrievePass
	set EncodedPass=
	if not defined User                        goto :EOF
	if     defined EncodedPass_%User%@%Domain% call :retrievePassHelper %%EncodedPass_%User%@%Domain%%%
	if     defined EncodedPass_%Domain%\%User% call :retrievePassHelper %%EncodedPass_%Domain%\%User%%%
	if     defined EncodedPass                 goto :EOF
	if     defined EncodedPass_%User%          call :retrievePassHelper %%EncodedPass_%User%%%
	                                           goto :EOF

:retrievePassHelper
	set EncodedPass=%1
	goto :EOF

:parseSwitches
  set switches=%*
  set switches=%switches:[Q]="""%
	set User=
	Set Domain=
	Set Password=
:parseSwitchesLoop
	set parseSwitches=%~1
	shift
	if not defined parseSwitches			 goto :EOF
	set parseSwitchesTest=%parseSwitches:"='%
	if /i "%parseSwitchesTest:~0,1%" NEQ "/"         goto :parseSwitchesLoop
	if /i "%parseSwitchesTest:~0,5%" EQU "/user"     set     User=%parseSwitches:~6%
	if /i "%parseSwitchesTest:~0,7%" EQU "/domain"   set   Domain=%parseSwitches:~8%
	if /i "%parseSwitchesTest:~0,9%" EQU "/password" set Password=%parseSwitches:~10%
	goto :parseSwitchesLoop

:len
	set /A len=0
	set lenCount=%*
:lenloop
	if defined lenCount (
		set lenCount=%lenCount:~1%
		set /A len += 1
		goto :lenloop
	)
	goto :EOF
	
	:sleep
        for /l %%a in (0,1,%1) do echo %%a && ping -n 1 -w 100 10.10.10.10 | find "nothing"
        goto :EOF

::######################################################################################
::######################################################################################
::######################################################################################
::######################################################################################




